#define DEFAULT_MQTT_HOST "mqtt1.eoh.io"
#define ERA_AUTH_TOKEN "a3f00619-a76e-41f7-8104-c3fc3891c81a"

#include <Arduino.h>
#include <ERa.hpp>
#include <Wire.h>
#include <LiquidCrystal_I2C.h>
#define byte uint8_t
#include <Keypad.h>
#include <DHT.h>

const char ssid[] = "Redmi Note";
const char pass[] = "1234567890";

// --- 1. CẤU HÌNH LCD 16x2 ---
LiquidCrystal_I2C lcd(0x27, 16, 2);

// --- 2. CẤU HÌNH MẬT KHẨU & TRẠNG THÁI ---
const String MASTER_PASS = "1234";
String inputPassword = "";

// Các biến trạng thái hệ thống
bool manualPumpState = false;
bool isAuthMode = false;
bool isMenuMode = false;
bool stopAlarm = false;

// Biến cho Slide Show
int currentPage = 0;
unsigned long lastPageSwitch = 0;
const int PAGE_DELAY = 3500;

// --- 3. CẤU HÌNH CẢM BIẾN ---
#define PIN_DHT_IN 34
#define PIN_DHT_OUT 25
#define DHTTYPE DHT11
DHT dhtIn(PIN_DHT_IN, DHTTYPE);
DHT dhtOut(PIN_DHT_OUT, DHTTYPE);

// Đo xăng (Siêu âm)
#define TRIG_PIN 32
#define ECHO_PIN 35
const int H_BE_CAN = 100;  // cm (Bể cạn)
const int H_BE_DAY = 5;    // cm (Bể đầy)
int currentFuelPct = 0;

#define MQ2_PIN 34
#define PIR_PIN 39

const int GAS_LIMIT = 1000;
const int TEMP_LIMIT = 55;

// --- 4. OUTPUT ---
#define RELAY_PIN 23
#define BUZZER_PIN 26
#define RED_LED 27
#define GREEN_LED 14


// --- 5. KEYPAD 4x3 ---
const byte ROWS = 4;
const byte COLS = 3;
char keys[ROWS][COLS] = {
  { '1', '2', '3' },
  { '4', '5', '6' },
  { '7', '8', '9' },
  { '*', '0', '#' }
};
byte rowPins[ROWS] = { 15, 4, 16, 17 };
byte colPins[COLS] = { 5, 18, 19 };
Keypad keypad = Keypad(makeKeymap(keys), rowPins, colPins, ROWS, COLS);

// --- BIẾN HỆ THỐNG ---
bool isFireDanger = false;  // Biến báo CHÁY
bool isIntruder = false;    // Biến báo CÓ NGƯỜI
unsigned long lastSensorTime = 0;

WiFiClient mbTcpClient;

#if defined(ERA_AUTOMATION)
#include <Automation/ERaSmart.hpp>

#if defined(ESP32) || defined(ESP8266)
#include <Time/ERaEspTime.hpp>
ERaEspTime syncTime;
#else
#define USE_BASE_TIME
#include <Time/ERaBaseTime.hpp>
ERaBaseTime syncTime;
#endif

ERaSmart smart(ERa, syncTime);
#endif

ERA_CONNECTED() {
  ERA_LOG(ERA_PSTR("ERa"), ERA_PSTR("ERa connected!"));
}

ERA_DISCONNECTED() {
  ERA_LOG(ERA_PSTR("ERa"), ERA_PSTR("ERa disconnected!"));
}

void timerEvent() {
  ERA_LOG(ERA_PSTR("Timer"), ERA_PSTR("Uptime: %d"), ERaMillis() / 1000L);
}

#if defined(USE_BASE_TIME)
unsigned long getTimeCallback() {
  return 0;
}
#endif

// ===== ERA INPUT VIRTUAL PINS =====

// V6 – Điều khiển máy bơm
ERA_WRITE(V6) {
  manualPumpState = param.getInt();

  // Không cho app tắt bơm khi đang cháy mà chưa tắt còi
  if (!isFireDanger || stopAlarm) {
    digitalWrite(RELAY_PIN, manualPumpState ? HIGH : LOW);
  }
}

// V7 – Tắt / bật còi tạm thời
ERA_WRITE(V7) {
  stopAlarm = param.getInt();

  if (stopAlarm) {
    digitalWrite(BUZZER_PIN, LOW);
  }
}


void setup() {
#if defined(ERA_DEBUG)
  Serial.begin(115200);
#endif

#if defined(USE_BASE_TIME)
  syncTime.setGetTimeCallback(getTimeCallback);
#endif

  ERa.setModbusClient(mbTcpClient);
  ERa.setScanWiFi(true);
  ERa.begin(ssid, pass);
  ERa.addInterval(1000L, timerEvent);

  Serial.begin(115200);

  lcd.init();
  lcd.backlight();
  lcd.setCursor(0, 0);
  lcd.print("HE THONG");
  lcd.setCursor(0, 1);
  lcd.print("KHOI DONG...");
  delay(2000);
  lcd.clear();

  dhtIn.begin();
  dhtOut.begin();
  pinMode(TRIG_PIN, OUTPUT);
  pinMode(ECHO_PIN, INPUT);
  pinMode(MQ2_PIN, INPUT);
  pinMode(PIR_PIN, INPUT);

  pinMode(RELAY_PIN, OUTPUT);
  pinMode(BUZZER_PIN, OUTPUT);
  pinMode(RED_LED, OUTPUT);
  pinMode(GREEN_LED, OUTPUT);

  digitalWrite(RELAY_PIN, LOW);
  digitalWrite(GREEN_LED, HIGH);
}

void loop() {
  ERa.run();
  // 1. Quét phím
  char key = keypad.getKey();
  if (key) {
    xuLyBanPhim(key);
    hienThiGiaoDien();
  }

  // 2. Quét cảm biến (0.5s/lần)
  if (millis() - lastSensorTime > 1500) {
    lastSensorTime = millis();
    docCamBienVaXuLy();
    if (!isAuthMode && !isMenuMode) hienThiGiaoDien();
  }

  // 3. Xử lý lật trang (Chỉ khi không báo động)
  if (!isAuthMode && !isMenuMode && !isFireDanger && !isIntruder && millis() - lastPageSwitch > PAGE_DELAY) {
    lastPageSwitch = millis();
    currentPage++;
    if (currentPage > 2) currentPage = 0;
    hienThiGiaoDien();
  }
}

// --- HÀM ĐO XĂNG ---
int docPhanTramXang() {
  long duration;
  float totalDistance = 0;
  int samples = 5;
  for (int i = 0; i < samples; i++) {
    digitalWrite(TRIG_PIN, LOW);
    delayMicroseconds(2);
    digitalWrite(TRIG_PIN, HIGH);
    delayMicroseconds(10);
    digitalWrite(TRIG_PIN, LOW);
    duration = pulseIn(ECHO_PIN, HIGH, 30000);
    if (duration == 0) totalDistance += H_BE_CAN;
    else totalDistance += (duration * 0.034 / 2);
    delay(5);
  }
  int pct = map((long)(totalDistance / samples), H_BE_CAN, H_BE_DAY, 0, 100);
  return constrain(pct, 0, 100);
}

void xuLyBanPhim(char key) {
  if (isMenuMode) {
    if (key == '1') {
    stopAlarm = !stopAlarm;
    ERa.virtualWrite(V7, stopAlarm);   // Đồng bộ app
  }
    else if (key == '2') {
      manualPumpState = !manualPumpState;
      ERa.virtualWrite(V6, manualPumpState);
      // Chỉ cho phép điều khiển bơm nếu không cháy (hoặc cháy mà đã tắt cảnh báo)
      if (!isFireDanger || (isFireDanger && stopAlarm)) {
        digitalWrite(RELAY_PIN, manualPumpState ? HIGH : LOW);
      }
    } else if (key == '#') {
      isMenuMode = false;
      lcd.clear();
    }
    return;
  }

  if (key == '*') {
    isAuthMode = true;
    inputPassword = "";
    lcd.clear();
  } else if (key == '#') {
    isAuthMode = false;
    inputPassword = "";
    lcd.clear();
  } else if (isAuthMode) {
    if (inputPassword.length() < 4) inputPassword += key;
    if (inputPassword.length() == 4) kiemTraMatKhau();
  }
}

void kiemTraMatKhau() {
  if (inputPassword == MASTER_PASS) {
    isAuthMode = false;
    isMenuMode = true;
    inputPassword = "";
    lcd.clear();
  } else {
    lcd.clear();
    lcd.print("SAI MAT KHAU!");
    digitalWrite(BUZZER_PIN, HIGH);
    delay(200);
    digitalWrite(BUZZER_PIN, LOW);
    delay(1000);
    isAuthMode = false;
    inputPassword = "";
    lcd.clear();
  }
}

void docCamBienVaXuLy() {
  float tIn = dhtIn.readTemperature();
  float tOut = dhtOut.readTemperature();
  if (isnan(tIn)) tIn = 0;
  if (isnan(tOut)) tOut = 0;
  int gasValue = analogRead(MQ2_PIN);
  currentFuelPct = docPhanTramXang();

  // Đọc cảm biến PIR
  bool motion = digitalRead(PIR_PIN);

  // LOGIC XÁC ĐỊNH TRẠNG THÁI
  if (gasValue > GAS_LIMIT || tIn > TEMP_LIMIT || tOut > TEMP_LIMIT) {
    isFireDanger = true;
    isIntruder = false;  // Ưu tiên báo cháy hơn báo trộm
  } else if (motion) {
    isFireDanger = false;
    isIntruder = true;  // Phát hiện có người
  } else {
    isFireDanger = false;
    isIntruder = false;
  }

  // --- XỬ LÝ OUTPUT ---

  // 1. NẾU CÓ CHÁY
  if (isFireDanger) {
    if (stopAlarm) {
      digitalWrite(BUZZER_PIN, LOW);                          // Tắt còi theo yêu cầu
      digitalWrite(RELAY_PIN, manualPumpState ? HIGH : LOW);  // Bơm theo manual
    } else {
      digitalWrite(BUZZER_PIN, HIGH);  // Hú còi
      digitalWrite(RELAY_PIN, HIGH);   // ÉP BẬT BƠM
    }
    digitalWrite(RED_LED, HIGH);
    digitalWrite(GREEN_LED, LOW);
  }
  // 2. NẾU CÓ NGƯỜI (NHƯNG KHÔNG CHÁY)
  else if (isIntruder) {
    if (stopAlarm) {
      digitalWrite(BUZZER_PIN, LOW);  // Tắt còi theo yêu cầu
    } else {
      digitalWrite(BUZZER_PIN, HIGH);  // Hú còi cảnh báo
    }
    digitalWrite(RELAY_PIN, manualPumpState ? HIGH : LOW);  // Bơm giữ nguyên (KHÔNG TỰ BẬT)
    digitalWrite(RED_LED, HIGH);                            // Đèn đỏ cảnh báo
    digitalWrite(GREEN_LED, LOW);
  }
  // 3. BÌNH THƯỜNG
  else {
    stopAlarm = false;  // Reset trạng thái tắt còi
    digitalWrite(RELAY_PIN, manualPumpState ? HIGH : LOW);
    digitalWrite(RED_LED, LOW);
    digitalWrite(GREEN_LED, HIGH);
    digitalWrite(BUZZER_PIN, LOW);
  }
    // ===== ERA OUTPUT VIRTUAL PINS =====
  ERa.virtualWrite(V0, tIn);              // Nhiệt độ trong
  ERa.virtualWrite(V1, tOut);             // Nhiệt độ ngoài
  ERa.virtualWrite(V2, gasValue);         // MQ2
  ERa.virtualWrite(V3, currentFuelPct);   // % xăng

  ERa.virtualWrite(V4, isFireDanger ? 1 : 0); // Cháy
  ERa.virtualWrite(V5, isIntruder ? 1 : 0);   // Có người

}

void hienThiGiaoDien() {
  // 1. MENU
  if (isMenuMode) {
    lcd.setCursor(0, 0);
    lcd.print("1.CanhBao:");
    lcd.print(stopAlarm ? "OFF " : "ON  ");
    lcd.setCursor(0, 1);
    lcd.print("2.Bom:");
    lcd.print(manualPumpState ? "ON " : "OFF");
    lcd.print("  #.Thoat");
    return;
  }
  // 2. PASSWORD
  if (isAuthMode) {
    lcd.setCursor(0, 0);
    lcd.print("NHAP MAT KHAU:  ");
    lcd.setCursor(0, 1);
    lcd.print(inputPassword);
    lcd.print("_               ");
    return;
  }

  // 3. BÁO ĐỘNG CHÁY (Ưu tiên cao nhất)
  if (isFireDanger) {
    lcd.setCursor(0, 0);
    if (stopAlarm) lcd.print("!! DA TAT COI !!");
    else lcd.print("!! NGUY HIEM !! ");
    lcd.setCursor(0, 1);
    lcd.print("GAS/NHIET CAO!  ");
    return;
  }

  // 4. BÁO ĐỘNG NGƯỜI (Ưu tiên nhì)
  if (isIntruder) {
    lcd.setCursor(0, 0);
    if (stopAlarm) lcd.print("!! DA TAT COI !!");
    else lcd.print("!! CANH BAO !!  ");
    lcd.setCursor(0, 1);
    lcd.print("CO NGUOI LAI GAN");  // Thông báo xâm nhập
    return;
  }

  // 5. SLIDE SHOW BÌNH THƯỜNG
  float tIn = dhtIn.readTemperature();
  float tOut = dhtOut.readTemperature();
  int gas = analogRead(MQ2_PIN);

  switch (currentPage) {
    case 0:
      lcd.clear();
      lcd.setCursor(0, 0);
      lcd.print("T.In: ");
      lcd.print((int)tIn);
      lcd.print("C       ");
      lcd.setCursor(0, 1);
      lcd.print("T.Out:");
      lcd.print((int)tOut);
      lcd.print("C       ");
      break;
    case 1:
      lcd.clear();
      lcd.setCursor(0, 0);
      lcd.print("Gas: ");
      lcd.print(gas);
      lcd.print("    ");
      lcd.setCursor(0, 1);
      lcd.print("Xang: ");
      lcd.print(currentFuelPct);
      lcd.print("%   ");
      break;
    case 2:
      lcd.clear();
      lcd.setCursor(0, 0);
      lcd.print("An ninh: ");
      lcd.print("AN TOAN ");
      lcd.setCursor(0, 1);
      lcd.print("Bom: ");
      if (manualPumpState) lcd.print("ON (Manual)");
      else lcd.print("OFF       ");
      break;
  }
}
